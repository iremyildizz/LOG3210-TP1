options {
	JAVA_UNICODE_ESCAPE=true;
	STATIC=false;					// 1 seul parseur actif a la fois (+ rapide)
	MULTI=true;						// type noeud = f(nom noeud)
	VISITOR=true;					// Cree une methode accept(visiteur)
}

//
// PARSER DEFINITION
//

PARSER_BEGIN(Parser)

package analyzer.ast;

public class Parser
{
	public static ASTProgram ParseTree(java.io.InputStream input) throws ParseException
	{
		Parser c = new Parser(input);
		return c.Program();
	}
}

PARSER_END(Parser)

//
// LEXICAL ANALYSIS
//

// White space

SKIP :
{
	< ( " " | "\t" | "\n" | "\r" )+	>
}

// Comments

MORE :
{
  "//" : IN_SINGLE_LINE_COMMENT
|
  <"/**" ~["/"]> { input_stream.backup(1); } : IN_FORMAL_COMMENT
|
  "/*" : IN_MULTI_LINE_COMMENT
}

<IN_SINGLE_LINE_COMMENT>
SPECIAL_TOKEN :
{
  <SINGLE_LINE_COMMENT: "\n" | "\r" | "\r\n" > : DEFAULT
}

<IN_FORMAL_COMMENT>
SPECIAL_TOKEN :
{
  <FORMAL_COMMENT: "*/" > : DEFAULT
}

<IN_MULTI_LINE_COMMENT>
SPECIAL_TOKEN :
{
  <MULTI_LINE_COMMENT: "*/" > : DEFAULT
}

<IN_SINGLE_LINE_COMMENT,IN_FORMAL_COMMENT,IN_MULTI_LINE_COMMENT>
MORE :
{
  < ~[] >
}




// Keywords
TOKEN :
{
	< IF: "if"	> |
	< ELSE: "else" > |
	< STRING: "string"> |
	< INT: "int"> |
	< FLOAT: "float" > |
	< BOOL: "bool"> |
	< PRINT: "print" > |
	< INPUT: "input" > |
	< IN: "in" > |
	< DO: "do" > |
	< WHILE: "while" > |
	< SWITCH: "switch" > |
	< CASE: "case" > |
    < DEF: "default">|
    < FOR: "for"> |
    < FUNC: "func"> |
    < RET: "return">
	// À compléter au besoin
}

// Operators
TOKEN :
{
	< ASSIGN: < EQUAL >  > |
	< COMPARE: < LESS > | < LESSEQUAL > | < GREAT > | <GREATEQUAL > | < DIFF > | < EQUALEQUAL > > |
    < LOGIC: < AND > | < OR > > |
	< #LESS: "<" > |
	< #LESSEQUAL: "<=" > |
	< #GREAT: ">" > |
	< #GREATEQUAL: ">=" > |
	< #DIFF: "!=" > |
	< #EQUALEQUAL: "==" > |

	< PLUS: "+" > |
	< MINUS: "-" > |
	< FOIS: "*" > |
	< DIV: "/" > |
    < NOT: "!" > |

	< #EQUAL: "=" > |
	< LPAREN: "(" > |
	< RPAREN: ")" > |
	< LACC: "{" > |
	< RACC: "}" > |
	< COLON: ":" > |
	< SEMICOLON: ";" > |
	< COMMA: "," > |
	< #AND: "&&" > |
	< #OR: "||" >
	// À compléter au besoin
}


// Identifiers and numbers
TOKEN :
{
	< IDENTIFIER: <LETTER> ( <LETTER> | <DIGIT> )* > |
	< #LETTER: ["a"-"z","A"-"Z","_"] > |
	< #DIGIT: ["0"-"9"] > |
	< INTEGER: ["1"-"9"] (["0"-"9"])* | "0" > |
	< REAL: (<INTEGER>)? (".") (<INTEGER>)? (<EXPONENT>)? > |
	< EXPONENT: (["e","E"]) (["+","-"])? <INTEGER> >
}




//
// SYNTAX ANALYSIS
//

ASTProgram Program() : { }
{
	Block() <EOF> { return jjtThis;  }
}

void Block() #void : { }
{
	 ( Stmt() )*
}


void Stmt() #void : { }
{
    IfStmt() |
    WhileStmt() |
    DoWhileStmt() |
    ForStmt() |
    FunctionStmt() |
	AssignStmt() <SEMICOLON>
}

void FunctionStmt() : { }
{
    <FUNC> Identifier() <LPAREN> ((Identifier() (<COMMA> Identifier())*) | (<SEMICOLON> <SEMICOLON>) )? <RPAREN> FunctionBlock()
}

void FunctionBlock() #void : { }
{
    <LACC> Block() (ReturnStmt())? <RACC> | Stmt()
}

void ReturnStmt() : { }
{
    <RET> (Expr())? <SEMICOLON>
}

void ForStmt() : { }
{
    <FOR> <LPAREN> (AssignStmt())? <SEMICOLON> (Expr())? <SEMICOLON> (AssignStmt())? <RPAREN> FunctionBlock()
}

void DoWhileStmt() : { }
{
    <DO> WhileBlock() <WHILE> <LPAREN> WhileCond() <RPAREN> <SEMICOLON>
}

void WhileStmt() : { }
{
    <WHILE> <LPAREN> WhileCond() <RPAREN> WhileBlock()
}

void WhileCond() : { }
{
    Expr()
}

void WhileBlock() : { }
{
    FunctionBlock()
}

void IfStmt() : { }
{
    <IF> <LPAREN> IfCond() <RPAREN> IfBlock() (LOOKAHEAD(2) ElseBlock())?
}

void ElseBlock() : { }
{
    <ELSE>
    (LOOKAHEAD(2) IfStmt() |
    FunctionBlock())
}

void IfCond() : { }
{
    Expr()
}

void IfBlock() : { }
{
    FunctionBlock()
}

void AssignStmt() : { Token t;}
{
	Identifier() <ASSIGN> Expr()
}


void Expr() #void : {}
{
    (AndOrExpr() (<LOGIC> AndOrExpr())*) #Logic(>1)
}

void AndOrExpr() #void : {}
{
    (ComparisonExpr() (LOOKAHEAD(2) <LOGIC> ComparisonExpr())*) #Logic(>1)
}

void ComparisonExpr() #void : {}
{
    (AddExpr() (<COMPARE> AddExpr())*) #Compare(>1)
}

void AddExpr() #void : {}
{
    (MulExpr() ((<PLUS>|<MINUS>) MulExpr())*)  #Addition(>1)
}

void MulExpr() #void : {}
{
    (NegationExpr() ((<FOIS> |<DIV>) NegationExpr())*)  #Multi(>1)
}

void NegationExpr() #void : {}
{
    <MINUS> NegationExpr() #Minus | NotExpr()
}

void NotExpr() #void : {}
{
    <NOT> NotExpr() #Not | BasicExpr()
}

void ParenExpr() #void : {}
{
    <LPAREN> Expr() <RPAREN>
}

void BasicExpr() #void : {}
{
    LOOKAHEAD(2) Identifier() |
    IntValue() |
    RealValue() |
    ParenExpr()
}






void Identifier() : { Token t;}
{
	t = <IDENTIFIER> {jjtThis.setValue(t.image);}
}

void IntValue() : { Token t;}
{
	t = <INTEGER> {jjtThis.setValue(Integer.parseInt(t.image));}
}

void RealValue() : { Token t;}
{
	t = <REAL> {jjtThis.setValue(Double.parseDouble(t.image));}
}
